# Maintainer: Vinícius dos Santos Oliveira <vini.ipsmaker@gmail.com>
pkgname=tufao-plugin
pkgver=0.0.4
pkgrel=2
pkgdesc="A QtCreator plugin to work with Tufão."
arch=('i686' 'x86_64')
url="http://tufao.googlecode.com/"
license=('LGPL3')
depends=('qtcreator=2.5.2')
source=('http://get.qt.nokia.com/qtcreator/qt-creator-2.5.2-src.tar.gz'
        'http://tufao.googlecode.com/files/qtcreatorplugin-0.0.4-src.tar.bz2')
md5sums=('4a9c09cdf4609753283c31451c84ceb8'
         'ed29641a8845061994fd8b74ebb5f9f5')

build() {
  if [ -z "$QTC_BUILD" ]; then
      if [ -z "$QTC_SOURCE" ]; then
          export QTC_SOURCE="$srcdir/qt-creator-2.5.2-src"
      fi
      cd "$QTC_SOURCE"

      mkdir -p build
      cd build
      qmake ../qtcreator.pro
      make

      export QTC_BUILD="$QTC_SOURCE/build"
  fi

  cd "$srcdir/qtcreatorplugin"
  qmake
  make
}

package() {
  if [ -z "$QTC_BUILD" ]; then
      if [ -z "$QTC_SOURCE" ]; then
          export QTC_SOURCE="$srcdir/qt-creator-2.5.2-src"
      fi

      export QTC_BUILD="$QTC_SOURCE/build"
  fi

  cd "${QTC_BUILD}/lib/qtcreator/plugins"
  mkdir -p "$pkgdir/usr/lib/qtcreator/plugins"
  cp -r TufaoTeam "$pkgdir/usr/lib/qtcreator/plugins"
}
